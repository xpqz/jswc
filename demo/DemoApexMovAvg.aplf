 DemoApexMovAvg connected;window;months;data;smoothed;series;options
⍝ Time series from https://github.com/yiyus/data-science-in-APL/blob/main/all.ipynb

 window←3                                              ⍝ Moving average window size
 months←¯1 12 ⎕DT 2↓¯10↓,(2018+⍳6)∘.,⍳12               ⍝ 2019-03 to 2024-02 in “Javascript data/time format”

 data←         291 272 278 346 284 283 364 262 283 340 ⍝ 2019
 data,←297 283 286 255 355 304 317 358 279 304 332 241 ⍝ 2020
 data,←410 264 244 243 299 240 236 318 276 302 228 224 ⍝ 2021
 data,←314 249 266 269 336 271 359 302 280 312 253 229 ⍝ 2022
 data,←342 265 263 321 252 294 372 287 269 315 257 318 ⍝ 2023
 data,←262 255                                         ⍝ 2024

 smoothed←window÷⍨window+/data                         ⍝ Compute the moving average

 series←(data 'Searches' months) (smoothed '3-month avg' ((window-1)↓months))
 ⍝ Triplets of (yvalues name xvalues)

 options←'{title: { text: "Searches for APL on Google", align: "center"},'
 options,←'yaxis:{type:"numeric",stepSize:25, decimalsInFloat: 0},'
 options,←'xaxis: { type: "datetime"},'
 options,←'animations:{enabled: false}}'

 'F1'eWC'Form' 'Charts'(50 50)(600 800)('Coord' 'Pixel')
 'F1.FILE' eWC 'Edit' '' (10 10)(23 300)
 'F1.SAVE' eWC 'Button' 'Save' (10 320)(23 30)('Event' 'Select' 'CBSaveChart' ('F1.FILE' 'F1.MOVAVG'))
 'F1.MOVAVG'eWC'ApexChart'(50 10)(540 780)('Series'series)('Options'options)
