 DemoTreeView connected;inputs;scores;folder;imagefiles;periods;z;⎕TRAP;m;players;val;i;s;imix;f;inout
 JSWC.CONNECTED←connected
 ⎕PW←1000

 n←10
 f←'+'
 allf←'+-×÷'

 imagefiles←'/images/'∘,¨'Folder-Closed.png' 'Folder-Open.png' 'Price-Ticket.png'
 'IL'eWC'ImageList'('Size'(16 16))('Masked' 2)('Files' imagefiles)
 'F1'eWC'Form' 'Function Table'(50 50)(800 800)('Coord' 'Pixel')

 'F1.MB'eWC'MenuBar'
 'F1.MB.File'eWC'Menu' '&File'
 'F1.MB.Col'eWC'Menu' '&Colours'
 'F1.MB.Col.Red'eWC'MenuItem' '&Red'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Col.Blue'eWC'MenuItem' '&Blue'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Col.Green'eWC'MenuItem' '&Green'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus'eWC'Menu' '&Focus'
 'F1.MB.Focus.TableSize'eWC'MenuItem' '&Size'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus.Function'eWC'MenuItem' '&Function'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus.CalcBtn'eWC'MenuItem' '&Calc Button'('Event' 'Select' 'JSWCUpdate')
 'F1.MB.Focus.Players'eWC'MenuItem' '&Player Table'('Event' 'Select' 'JSWCUpdate')

 'F1.Players'eWC'Grid'('ColTitles'('Name' 'Gender' 'Score' 'Expert'))('Posn'(10 10))('Size'(100 480))('TitleWidth' 0)('ShowInput' 1)('Event' 'CellChanged' 'JSWCUpdate')
 'F1.Players.Name'eWC'Edit'('Size'(23 100))
 'F1.Players.Gender'eWC'Combo'('Items'('Male' 'Female'))('Size'(10 100))
 'F1.Players.Score'eWC'Edit'('FieldType' 'Numeric')('Size'(23 50))
 'F1.Players.Expert'eWC'Button'('Style' 'Check')('Size'(23 25))
 inputs←'F1.Players.'∘,¨'Name' 'Gender' 'Score' 'Expert'
 scores←12 13
 players←'Amir' 'Fatima','Male' 'Female',scores,⍪0 1
 'F1.Players'eWS('Values' players)('CellTypes'(2 4⍴1 2 3 4))('Input'inputs)
 'F1.Average'eWC'Label'('Average Score:',1⍕+/scores÷≢scores)(115 100)(23 400)
 periods←(⊂'Q1'), (,'Jan' 'Feb' 'Mar',inout), (⊂'Q2'),,'Apr' 'May' 'Jun',inout←3 2⍴'In' 'Out'
 'F1.Months'eWC'TreeView' periods (10 500)(150 150)('Depth'(20⍴0,9⍴1 2 2))('ImageListObj' '#.IL')('ImageIndex' (imix←20⍴1,9⍴1 3 3))('Event' ('Expanding' 'Retracting' 'KeyPress' 'ItemDown' 'ItemDblClick' 'MouseUp') 'JSWCUpdate')

 'F1.TableSize'eWC'Edit' '10'(210 10)(23 100)('FieldType' 'Numeric')('Event' 'Change' 'JSWCUpdate')
 'F1.Function'eWC'Combo'(,¨allf)f(210 120)(23 100)('SelItems'(allf=f))('Event' 'Select' 'JSWCUpdate')
 'F1.CalcBtn'eWC'Button' 'Calc'(210 230)(23 100)('Event' 'Select' 'JSWCUpdate')
 'F1.Table'eWC'Grid'(∘.×⍨⍳n)(240 10)(550 780)

 'F1.PasswordField'eWC'Edit' 'Password'(210 340)(23 100)('Password' '*')('Event' 'Change' 'JSWCUpdate')

 eNQ 'F1.TableSize' 'GotFocus'
 eNQ 'F1.Months' 'GotFocus'
 eNQ 'F1.Months' 'ItemDown' 1 1 0 4

 ⍝ Test eWG

  (s i)←'F1.Months' eWG 'SelItems' 'ImageIndex' ⍝ Static only
  :If i≢imix
      ⎕←'*** Unexpected return value for F1.Months ImageIndex'
      ⎕←i
  :EndIf
  :If s≢20⍴0
      ⎕←'*** Unexpected return value for F1.Months SelItems'
      ⎕←s
  :EndIf

  z←'F1.Function' eWG f←'Event' 'SelItems' 'Text' ⍝ Mix of Dynamic and Static
  :If ∨/m←z≢¨(,⊂'Select' (,⊂'JSWCUpdate')) (1 0 0 0)(,'+')
      ⎕←'*** Unexpected return value for F1.Function: ',⍕m/f
      ⎕←m/z
  :EndIf

  z←'F1.TableSize' eWG f←'text' 'VALUE' ⍝ Both Dynamic
  :If ∨/m←z≢¨'10' 10
      ⎕←'*** Unexpected return value for F1.TableSize: ',⍕m/f
      ⎕←z
  :EndIf

  z←'F1.Players' eWG 'Values' ⍝ Dynamic
  :If z≢players
      ⎕←'*** Unexpected return value for F1.Players.Values'
      ⎕←z
  :EndIf

  ⍝ Test Edit
  'F1.TableSize' eWS 'Text' '11'
  val←'F1.TableSize' eWG 'Value'
  :If val≢11
     ⎕←'*** After setting Text to "11", TableSize.Value is ',⍕val
  :EndIf

  'F1.TableSize' eWS 'Value' 10
  val←'F1.TableSize' eWG 'Text'
  :If val≢,'10'
     ⎕←'*** After setting Value to 10, TableSize.Text is ',⍕val
  :EndIf

  'F1.Function' eWS 'SelItems' (m←allf=f←'×')
  val←'F1.Function' eWG 'Text'
  :If val≢,f
     ⎕←'*** After setting SelItems to ',(⍕m),' Function.Text is ',val
  :EndIf

  :Trap 6
      val←'F1.Function' eWG 'NoSuchProperty'
      ⎕←'*** Client failed to reject WG of "NoSuchProperty"'
  :EndTrap
